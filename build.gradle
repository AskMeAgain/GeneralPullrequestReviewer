plugins {
    id 'org.jetbrains.intellij' version '1.5.2'
    id 'java'
}

group 'intellij.plugin.template'
version System.getenv("RELEASE_VERSION")

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'org.apache.commons:commons-lang3:3.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'io.github.openfeign:feign-okhttp:10.11'
    implementation 'io.github.openfeign:feign-jackson:10.11'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

intellij {
    updateSinceUntilBuild = false
    version = '2021.3.2'
    plugins = ['com.intellij.java']
}

patchPluginXml {
    version = project.getVersion()
    changeNotes = new File("marketplace-changenotes.html").text
    pluginDescription = new File("marketplace-description.html").text
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

signPlugin {
    certificateChain = System.getenv("CERTIFICATE_CHAIN")
    privateKey = System.getenv("PRIVATE_KEY")
    password = System.getenv("PRIVATE_KEY_PASSWORD")
}

publishPlugin {
    token = System.getenv("PUBLISH_TOKEN")
    channels = ["unstable"]
}
